//Standard Installation

/*
Variables + quest/item stuff for BG2EE
*/


ACTION_IF (GAME_INCLUDES ~bg2~) BEGIN


//Set Global variables (check if party has items)
<<<<<<<< .../temp/variables
IF
	PartyHasItem("misc5o")
	!Global("d2HasSilverPants","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2HasSilverPants","GLOBAL",1)
		Continue()
END

IF
	PartyHasItem("misc47")
	!Global("d2HasGoldPants","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2HasGoldPants","GLOBAL",1)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/variables~  ~%workspace%/variables.BAF~
  EVALUATE_BUFFER

ACTION_FOR_EACH area IN ~AR0700~ ~AR3000~ ~AR4500~ ~AR0400~ BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%area%.BCS~) BEGIN
    EXTEND_TOP ~%area%.BCS~  ~%workspace%/variables.BAF~
  END
END



//Add Gold Pantaloons if not obtained in starting dungeon
<<<<<<<< .../temp/pantaloons
IF
	!Global("d2HasGoldPants","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2HasGoldPants","GLOBAL",1)
		ActionOverride("Shelf1",CreateItem("misc47",0,0,0))
		Continue()
END
>>>>>>>>

COPY - ~.../temp/pantaloons~  ~%workspace%/pantaloons.BAF~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~AR3001.BCS~    ~%workspace%/pantaloons.BAF~



//Add Silver Pantaloons if not obtained in SoA
<<<<<<<< .../temp/silver
IF
	!Global("d2HasSilverPants","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2HasSilverPants","GLOBAL",1)
		ActionOverride("Container1",CreateItem("misc5o",0,0,0))
		Continue()
END
>>>>>>>>

COPY - ~.../temp/silver~      ~%workspace%/silver.BAF~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~AR4500.BCS~    ~%workspace%/silver.BAF~



//Journal entry for Repair Manual
<<<<<<<< .../temp/repair
IF
	PartyHasItem("%repairbook%")
	Global("d2RibaldBigMetal","GLOBAL",1)
	!Global("d2HasRepairManual","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2HasRepairManual","GLOBAL",1)
		AddJournalEntry(@40002,QUEST)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/repair~  ~%workspace%/repair.BAF~
  EVALUATE_BUFFER

EXTEND_TOP ~AR0702.BCS~  ~%workspace%/repair.BAF~
EXTEND_TOP ~AR0411.BCS~  ~%workspace%/repair.BAF~



//Fix Rod (fade black)
<<<<<<<< .../temp/d2tecfix
IF
	True()
THEN
	RESPONSE #100
		Wait(1)
		FadeToColor([45.0],0)
		Wait(2)
		FadeFromColor([45.0],0)
		Wait(1)
		ActionOverride("ribald",StartDialogueNoSet(LastTalkedToBy))
END
>>>>>>>>

COPY - ~.../temp/d2tecfix~  ~%workspace%/d2tecfix.BAF~
  EVALUATE_BUFFER
COMPILE ~%workspace%/d2tecfix.BAF~



//Pulse III
<<<<<<<< .../temp/cespenar
IF
	Global("d2CespenarBigMetal","GLOBAL",1)
	Global("ImpForgeStuff","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2CespenarBigMetal","GLOBAL",2)
		SetGlobal("ImpForgeStuff","GLOBAL",0)
		SetGlobal("ItemMaker","GLOBAL",999)
		GiveItemCreate("%pulseIII%",Player1,0,0,0)
		StartDialogueNoSet(Player1)
END
>>>>>>>>

COPY - ~.../temp/cespenar~    ~%workspace%/cespenar.BAF~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~botsmith.BCS~  ~%workspace%/cespenar.BAF~


END


/*
*/