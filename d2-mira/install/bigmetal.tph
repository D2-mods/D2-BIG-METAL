//
//


//functions for adding text

//Big Metal items
DEFINE_ACTION_FUNCTION d2mira_bigmetal_text BEGIN
  COPY_EXISTING ~%bigmetalrod%.ITM~  ~override~
    SAY NAME1 ~%metalrod_name%~ SAY UNIDENTIFIED_DESC ~%metalrod_desc%~
    SAY NAME2 ~%metalrod_name%~ SAY DESC ~%metalrod_desc%~
  COPY_EXISTING ~%pulse%.ITM~  ~override~
    SAY NAME1 ~%pulse_name%~ SAY UNIDENTIFIED_DESC ~%pulse_desc%~
    SAY NAME2 ~%pulse_name%~ SAY DESC ~%pulse_desc%~
  COPY_EXISTING ~%frag%.ITM~  ~override~
    SAY NAME1 ~%frag_name%~ SAY UNIDENTIFIED_DESC ~%frag_desc%~
    SAY NAME2 ~%frag_name%~ SAY DESC ~%frag_desc%~
  COPY_EXISTING ~%scorcher%.ITM~  ~override~
    SAY NAME1 ~%scorcher_name%~ SAY UNIDENTIFIED_DESC ~%scorcher_desc%~
    SAY NAME2 ~%scorcher_name%~ SAY DESC ~%scorcher_desc%~
  COPY_EXISTING ~%bigmetalunit%.ITM~  ~override~
    SAY NAME1 ~%combinedpants_name%~ SAY UNIDENTIFIED_DESC ~%combinedpants_desc%~
    SAY NAME2 ~%metalunit_name%~ SAY DESC ~%metalunit_desc%~
  COPY_EXISTING ~%prototype%.ITM~  ~override~
    SAY NAME1 ~%prototype_name%~ SAY UNIDENTIFIED_DESC ~%prototype_desc%~
    SAY NAME2 ~%prototype_name%~ SAY DESC ~%prototype_desc%~
  COPY_EXISTING ~%pulseIII%.ITM~  ~override~
    SAY NAME1 ~%pulseIII_name%~ SAY UNIDENTIFIED_DESC ~%pulseIII_desc%~
    SAY NAME2 ~%pulseIII_name%~ SAY DESC ~%pulseIII_desc%~
END

//
//Pantaloons
DEFINE_ACTION_FUNCTION d2mira_pantaloons_text BEGIN
  COPY_EXISTING ~%bronze%.ITM~  ~override~							
    SAY NAME1 ~%bronzepants_name%~ SAY UNIDENTIFIED_DESC ~%bronzepants_desc%~ 
    SAY NAME2 ~%bronzepants_name%~ SAY DESC ~%bronzepants_desc%~
  COPY_EXISTING ~%silver%.ITM~  ~override~							
    SAY NAME1 ~%silverpants_name%~ SAY UNIDENTIFIED_DESC ~%silverpants_desc%~ 
    SAY NAME2 ~%silverpants_name%~ SAY DESC ~%silverpants_desc%~
  COPY_EXISTING ~%gold%.ITM~  ~override~							
    SAY NAME1 ~%goldenpants_name%~ SAY UNIDENTIFIED_DESC ~%goldenpants_desc_unidentified%~ 
    SAY NAME2 ~%goldenpants_name%~ SAY DESC ~%goldenpants_desc%~
END

//
//for BG1 (different text)
DEFINE_ACTION_FUNCTION d2mira_bg1alt_text BEGIN
  COPY_EXISTING ~%bigmetalrod%.ITM~  ~override/%metalrodb%.ITM~
    SAY NAME1 ~%metalrod_name%~ SAY UNIDENTIFIED_DESC ~%metalrod_desc_bg1%~
    SAY NAME2 ~%metalrod_name%~ SAY DESC ~%metalrod_desc_bg1%~
    WRITE_LONG 0x34 300    // 300 gold

  COPY_EXISTING ~%prototype%.ITM~  ~override/%prototypeb%.ITM~
    SAY NAME1 ~%prototype_name%~ SAY UNIDENTIFIED_DESC ~%prototype_desc_bg1%~
    SAY NAME2 ~%prototype_name%~ SAY DESC ~%prototype_desc_bg1%~
END

//
//Quest items (BG2)
DEFINE_ACTION_FUNCTION d2mira_bg2quest_text BEGIN
  COPY_EXISTING ~%bigmetalrod%.ITM~  ~override/%brokenrod%.ITM~
    SAY NAME1 ~%questitembg2_name%~ SAY UNIDENTIFIED_DESC ~%questitembg2_desc%~
    SAY NAME2 ~%questitembg2_name%~ SAY DESC ~%questitembg2_desc%~
    LPF DELETE_ITEM_HEADER INT_VAR header_type=~-1~ END
    WRITE_SHORT 0x1c 0    // change item type
    WRITE_LONG  0x34 0    // 0 gold

  COPY ~%files%/repairmanual.ITM~  ~override/%repairbook%.ITM~
    SAY NAME1 ~%questbookbg2_name%~ SAY UNIDENTIFIED_DESC ~%questbookbg2_desc%~
    SAY NAME2 ~%questbookbg2_name%~ SAY DESC ~%questbookbg2_desc%~
END

//
//for classic BG2 engine
DEFINE_ACTION_FUNCTION d2mira_bg2core BEGIN
  COPY ~%resource%/SECRET03.ITM~  ~override~    // make slightly more like EEs
       ~%resource%/SECRET04.ITM~  ~override~

  ACTION_BASH_FOR ~%resource%~  ~.*~ BEGIN
    ACTION_IF !(FILE_EXISTS_IN_GAME ~%BASH_FOR_FILE%~) BEGIN    // add files if missing
      COPY ~%BASH_FOR_FILESPEC%~  ~override~
    END
  END

  ACTION_PHP_EACH update_items AS mod => base BEGIN
    ACTION_IF (FILE_EXISTS_IN_GAME ~%base%.ITM~) BEGIN    // use BG2 files as defaults
      COPY_EXISTING ~%base%.ITM~  ~override/%mod%.ITM~
        LPF ALTER_EFFECT INT_VAR silent=1 STR_VAR match_resource=~%SOURCE_RES%~ resource=~%DEST_RES%~ END
    END
  END
END



//
//Install items

//BGEE, BG2EE
ACTION_IF (GAME_IS ~bgee bg2ee eet~) BEGIN
  COPY ~%data%~  ~override~
  COPY ~%bgee%~  ~override~
  LAF d2mira_bigmetal_text END
  LAF d2mira_bg1alt_text END
  LAF d2mira_bg2quest_text END
END

//BG2
ACTION_IF (GAME_IS ~bg2 tob tutu tutu_totsc ca~) BEGIN
  COPY ~%iwd1%~  ~override~
  LAF d2mira_bg2core END
  LAF d2mira_bigmetal_text END
  LAF d2mira_bg1alt_text END
  LAF d2mira_bg2quest_text END
END

//IWDEE
ACTION_IF (GAME_IS ~iwdee~) BEGIN
  COPY ~%data%~  ~override~
  COPY ~%extra%~  ~override~
  LAF d2mira_bigmetal_text END
  LAF d2mira_pantaloons_text END
END

//IWD1
ACTION_IF (GAME_IS ~iwd how totlm~) BEGIN
  COPY ~%iwd1%~  ~override~
  COPY ~%extra%~  ~override~
  LAF d2mira_bigmetal_text END
  LAF d2mira_pantaloons_text END
END

//IWD2
ACTION_IF (GAME_IS ~iwd2~) BEGIN
  COPY ~%iwd2%~  ~override~
  COPY ~%extra%~  ~override~
  LAF d2mira_bigmetal_text END
  LAF d2mira_pantaloons_text END
END

//IWD-in-BG2
ACTION_IF (GAME_IS ~iwd_in_bg2~) BEGIN
  COPY ~%iwd1%~  ~override~
  LAF d2mira_bg2core END
  LAF d2mira_bigmetal_text END
  LAF d2mira_pantaloons_text END
END


//elemental weapons (for Better IWD Pregen scripts)
LAF d2_elemental_weapon_arrays STR_VAR item=~%frag%~ element=~fire~ RET_ARRAY d2fireweap END
LAF d2_elemental_weapon_arrays STR_VAR item=~%scorcher%~ element=~fire~ RET_ARRAY d2fireweap END


/*
*/