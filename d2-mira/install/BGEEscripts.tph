//Standard Installation

/*
Variables, journal entry, store items for BGEE
*/


ACTION_IF (GAME_INCLUDES ~bg1~) BEGIN


//Set Global variables (check if party has items)
<<<<<<<< .../temp/variables
IF
	OR(2)
		PartyHasItem("%metalrodb%")
		PartyHasItem("%bigmetalrod%")
	!Global("d2HasBigMetalRod","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2HasBigMetalRod","GLOBAL",1)
		Continue()
END

IF
	OR(2)
		PartyHasItem("%prototypeb%")
		PartyHasItem("%prototype%")
	!Global("d2HasPrototype","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2HasPrototype","GLOBAL",1)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/variables~  ~%workspace%/variables.BAF~
  EVALUATE_BUFFER

EXTEND_TOP ~%baldursgate%.BCS~  ~%workspace%/variables.BAF~
EXTEND_TOP ~%candlekeep%.BCS~  ~%workspace%/variables.BAF~



//
//BG1: Black Lily journal entry
<<<<<<<< .../temp/blacklily
IF
	Global("d2BlackLilyTalk","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2BlackLilyTalk","GLOBAL",2)
		SetGlobalTimer("d2JournalDelay","GLOBAL",3)
		Continue()
END

IF
	Global("d2BlackLilyTalk","GLOBAL",2)
	!GlobalTimerNotExpired("d2JournalDelay","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("d2BlackLilyTalk","GLOBAL",3)
		AddJournalEntry(@30001,INFO)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/blacklily~  ~%workspace%/blacklily.BAF~
  EVALUATE_BUFFER

EXTEND_TOP ~%baldursgate%.BCS~  ~%workspace%/blacklily.BAF~
EXTEND_TOP ~%thiefguild%.BCS~  ~%workspace%/blacklily.BAF~



//
//BGEE: add Metal Rod (note: AddStoreItem is EE-only)

/*add to store by script (old version)
ACTION_IF (RESOURCE_CONTAINS ~ACTION.IDS~ ~AddStoreItem~) BEGIN

<<<<<<<< .../temp/luckystore
IF
	GlobalGT("d2BlackLilyTalk","GLOBAL",0)
	!Global("d2LuckyAelloBigMetal","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2LuckyAelloBigMetal","GLOBAL",1)
		AddStoreItem("STOCHEAP","%metalrodb%",1,1)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/luckystore~  ~%workspace%/luckystore.BAF~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~%baldursgate%.BCS~  ~%workspace%/luckystore.BAF~
EXTEND_BOTTOM ~%luckystore%.BCS~  ~%workspace%/luckystore.BAF~

END
*/

//
//add to container in store (new version)
<<<<<<<< .../temp/luckybarrel
IF
	GlobalGT("d2BlackLilyTalk","GLOBAL",0)
	!Global("d2LuckyAelloBigMetal","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2LuckyAelloBigMetal","GLOBAL",1)
		ActionOverride("Container3",CreateItem("%metalrodb%",0,0,0))    // Container3 is non-trapped
		Continue()
END
>>>>>>>>

COPY - ~.../temp/luckybarrel~  ~%workspace%/luckybarrel.BAF~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~%luckystore%.BCS~  ~%workspace%/luckybarrel.BAF~

//check if area has script set
ACTION_IF (FILE_EXISTS_IN_GAME ~%luckystore%.ARE~) BEGIN
  COPY_EXISTING ~%luckystore%.ARE~  ~override~
    WRITE_ASCII 0x94 ~%luckystore%~ #8
  BUT_ONLY
END



//
//SoD: Journal entry
<<<<<<<< .../temp/sodhalbaz
IF
	Global("d2HalbazBigMetal","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2HalbazBigMetal","GLOBAL",2)
		SetGlobalTimer("d2JournalDelay","GLOBAL",5)
		Continue()
END

IF
	!Global("d2HasPrototype","GLOBAL",1)
	Global("d2HalbazBigMetal","GLOBAL",2)
	!GlobalTimerNotExpired("d2JournalDelay","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("d2HalbazBigMetal","GLOBAL",3)
		AddJournalEntry(@30002,INFO)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/sodhalbaz~  ~%workspace%/sodhalbaz.BAF~
  EVALUATE_BUFFER

ACTION_IF (FILE_EXISTS_IN_GAME ~bd0020.ARE~) BEGIN
  EXTEND_TOP ~bd0020.BCS~  ~%workspace%/sodhalbaz.BAF~
  EXTEND_TOP ~bd0020.BCS~  ~%workspace%/variables.BAF~
END



//
//SoD: Add Rod and/or Pulse (prototype)
<<<<<<<< .../temp/RodPulse
IF
	!PartyHasItem("%metalrodb%")
	!PartyHasItem("%bigmetalrod%")
	!Global("d2HasBigMetalRod","GLOBAL",1)
	!Global("d2BelegarmBigMetal","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2BelegarmBigMetal","GLOBAL",1)
		AddStoreItem("bdbelega","%bigmetalrod%",1,1)
		Continue()
END

IF
	!PartyHasItem("%prototypeb%")
	!PartyHasItem("%prototype%")
	!Global("d2HasPrototype","GLOBAL",1)
	!Global("d2BelegarmPrototype","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2BelegarmPrototype","GLOBAL",1)
		AddStoreItem("bdbelega","%prototype%",1,1)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/RodPulse~  ~%workspace%/siegecamp.BAF~
  EVALUATE_BUFFER

ACTION_IF (FILE_EXISTS_IN_GAME ~bd3000.ARE~) BEGIN
  EXTEND_TOP ~bd3000.BCS~ ~%workspace%/siegecamp.BAF~
  EXTEND_TOP ~bd3000.BCS~ ~%workspace%/variables.BAF~
END


END


/*
*/